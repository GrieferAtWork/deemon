/* Copyright (c) 2018-2026 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2018-2026 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */

import * from deemon;
import * from errors;
import * from math;
import * from util;

global class point {
	public member x: Numeric;
	public member y: Numeric;

	this(x: Numeric, y: Numeric) {
		this.x = x;
		this.y = y;
	}

	copy(other: point) {
		x = other.x;
		y = other.y;
	}
	operator = (other: point) {
		x = other.x;
		y = other.y;
	}

	/* `x || y' would have made more sense, but
	 * this is only for backwards compatibility */
	operator bool(): bool {
		return x && y;
	}

	operator str(): string {
		return f"({x}, {y})";
	}
	operator repr(): string {
		return f"point({repr x}, {repr y})";
	}

	operator + (other: point) -> point(x + other.x, y + other.y);
	operator - (other: point) -> point(x - other.x, y - other.y);
	operator * (other: Numeric) -> point(x * other, y * other);
	operator / (other: Numeric) -> point(x / other, y / other);
	operator += (other: point) { x += other.x; y += other.y; return this; }
	operator -= (other: point) { x -= other.x; y -= other.y; return this; }
	operator *= (other: Numeric) { x *= other; y *= other; return this; }
	operator /= (other: Numeric) { x /= other; y /= other; return this; }

	function distance(to: point): float -> sqrt(distance_sqr(to));
	function distance_sqr(to: point): float -> sqr(to.x - x) + sqr(to.y - y);
	function project(angle: float, distance: float): point {
		local s, c = sincos(angle)...;
		return point(x + s * distance, y + c * distance);
	}
	function bearing(to: point): float -> atan2(to.x - x, to.y - y);
}

global class rect {

	public member xmin: Numeric;
	public member ymin: Numeric;
	public member xmax: Numeric;
	public member ymax: Numeric;

	@[gatw.doc("()")]
	@[gatw.doc("(min:?Gpoint,max:?Gpoint)")]
	@[gatw.doc("(xmin:?DNumeric,xmax:?DNumeric,ymin:?DNumeric,ymax:?DNumeric)")]
	this(args...) {
		switch (#args) {
		case 0:
			xmin = none;
			xmax = none;
			ymin = none;
			ymax = none;
			break;
		case 2: {
			local min = args[0];
			local max = args[1];
			xmin = min.x;
			ymin = min.y;
			xmax = max.x;
			ymax = max.y;
		}	break;
		case 4:
			xmin = args[0];
#if 1 /* True deemon 101+ has the order (xmin, xmax, ymin, ymax).
       * However, since everything (including other functions within this file)
       * assumed the argument order (xmin, ymin, xmax, ymax), we fix that here,
       * even though it was broken in the original */
			ymin = args[1];
			xmax = args[2];
#else
			xmax = args[1];
			ymin = args[2];
#endif
			ymax = args[3];
			break;
		default:
			throw TypeError(f"rect() expected 0, 2 or 4 arguments when {#args} were given");
		}
	}

	copy(other: rect) {
		xmin = other.xmin;
		ymin = other.ymin;
		xmax = other.xmax;
		ymax = other.ymax;
	}

	operator = (other: rect) {
		xmin = other.xmin;
		xmax = other.xmax;
		ymin = other.ymin;
		ymax = other.ymax;
	}

	property sizex: Numeric = {
		get(): Numeric {
			return abs(xmax - xmin);
		}
		set(x: Numeric) {
			xmax = xmin + x;
		}
	}
	property sizey: Numeric = {
		get(): Numeric {
			return abs(ymax - ymin);
		}
		set(y: Numeric) {
			ymax = ymin + y;
		}
	}
	property size: point = {
		get(): point {
			return point(sizex, sizey);
		}
		set(size: point) {
			sizex = size.x;
			sizey = size.y;
		}
	}
	property min: point = {
		get(): point {
			return point(xmin, ymin);
		}
		set(min: point) {
			xmin = min.x;
			ymin = min.y;
		}
	}
	property max: point = {
		get(): point {
			return point(xmax, ymax);
		}
		set(max: point) {
			xmax = max.x;
			ymax = max.y;
		}
	}

	operator bool(): bool {
		return xmax != xmin && ymax != ymin;
	}

	operator str(): string {
		return f"(({xmin}, {ymin}), ({xmax}, {ymax}))";
	}
	operator repr(): string {
		return f"rect(point({repr xmin}, {repr ymin}), point({repr xmax}, {repr ymax}))";
	}
	operator + (other: rect | point): rect {
		if (other is rect) {
			return rect(xmin + other.xmin, ymin + other.ymin,
			            xmax + other.xmax, ymax + other.ymax);
		}
		return rect(xmin + other.x, ymin + other.y,
		            xmax + other.x, ymax + other.y);
	}
	operator - (other: rect | point): rect {
		if (other is rect) {
			return rect(xmin - other.xmin, ymin - other.ymin,
			            xmax - other.xmax, ymax - other.ymax);
		}
		return rect(xmin - other.x, ymin - other.y,
		            xmax - other.x, ymax - other.y);
	}
	operator += (other: rect | point): rect {
		if (other is rect) {
			xmin += other.xmin;
			ymin += other.ymin;
			xmax += other.xmax;
			ymax += other.ymax;
		} else {
			local x = other.x;
			local y = other.y;
			xmin += x;
			ymin += y;
			xmax += x;
			ymax += y;
		}
		return this;
	}
	operator -= (other: rect | point): rect {
		if (other is rect) {
			xmin -= other.xmin;
			ymin -= other.ymin;
			xmax -= other.xmax;
			ymax -= other.ymax;
		} else {
			local x = other.x;
			local y = other.y;
			xmin -= x;
			ymin -= y;
			xmax -= x;
			ymax -= y;
		}
		return this;
	}

	operator * (other: point): rect {
		return rect(xmin * other.x, ymin * other.y,
		            xmax * other.x, ymax * other.y);
	}
	operator / (other: point): rect {
		return rect(xmin / other.x, ymin / other.y,
		            xmax / other.x, ymax / other.y);
	}
	operator *= (other: point): rect {
		local x = other.x;
		local y = other.y;
		xmin *= x;
		ymin *= y;
		xmax *= x;
		ymax *= y;
		return this;
	}
	operator /= (other: point): rect {
		local x = other.x;
		local y = other.y;
		xmin /= x;
		ymin /= y;
		xmax /= x;
		ymax /= y;
		return this;
	}

	operator contains(other: rect | point): bool {
		if (other is rect) {
			return other.xmin >= xmin && other.xmax < xmax &&
			       other.ymin >= ymin && other.ymax < ymax;
		}
		local x = other.x;
		local y = other.y;
		return x >= xmin && x < xmax &&
		       y >= ymin && y < ymax;
	}

}
