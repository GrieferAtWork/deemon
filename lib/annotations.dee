/* Copyright (c) 2018-2024 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2018-2024 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */

import * from deemon;

@@An annotation that may be used to overload a function by its arguments count
@@>import Overload from annotations;
@@>
@@>@Overload
@@>function foobar(x) {
@@>	print "Called with 1 argument:", x;
@@>}
@@>@foobar.register
@@>function foobar(x, y) {
@@>	print "Called with 2 arguments:", x, y;
@@>}
@@>foobar(10);     /* First variant */
@@>foobar(10, 20); /* Second variant */
class Overload: Callable {
	private member primary: Callable;
	private member overloads: {Callable...} = [];

	this(func: Callable)
		: primary(func)
	{}

	public operator str(): string {
		return f"<Overload: {primary}>";
	}
	public operator repr(fp) {
		print fp: ("Overload(", repr primary, ")"),;
		local invload = invalidOverload;
		for (local x: overloads) {
			if (x == invload)
				continue;
			print fp: (".register(", repr x, ")"),;
		}
	}

	private function invalidOverload(args...) {
		throw Error.TypeError(f"function {primary} does not accept {#args} args");
	}

	public operator () (args..., **kwds) {
		__stack local argc = #args;
		if (argc < #overloads)
			return overloads[argc](args...);
		return primary(args..., **kwds);
	}

	@@Register a new overload to-be considered for invocation
	public function register(func: Function): Overload {
		__stack local min = func.__argc_min__;
		__stack local max = func.__argc_max__;
		__stack local overloads = this.overloads;
		__stack local count = #overloads;
		if (max >= count) {
			overloads.resize(max + 1, invalidOverload);
			__stack local primary = this.primary;
			overloads.fill(primary.__argc_min__, primary.__argc_max__ + 1, primary);
		}
		for (__stack local i: [min:max + 1])
			overloads[i] = func;
		return this;
	}
}



@@An annotation that may be used to mark a function as deprecated, and have a warning be printed to :File.stderr the first time it is invoked
@@>import Deprecated from annotations;
@@>
@@>@Deprecated
@@>function mydepr() {
@@>	print "In mydepr()";
@@>}
@@>@Deprecated("Don't call me!")
@@>function mydepr2() {
@@>	print "In mydepr2()";
@@>}
@@>
@@>mydepr();  /* ... : Deprecated function `mydepr' called */
@@>mydepr2(); /* ... : Deprecated function `mydepr2' called: Don't call me! */
class Deprecated: Callable {
	private member m_func: Callable;
	private member m_message: string;
	private member m_didcall: bool = false;

	this(func: Callable, reason: string = "") {
		m_func = func;
		if (reason)
			m_message = reason;
	}

	public operator str(): string {
		return f"<Deprecated {m_func}>";
	}
	public operator repr(): string {
		if (m_message is bound)
			return f"Deprecated({repr m_func}, {repr m_message})";
		return f"Deprecated({repr m_func})";
	}

	public operator () (args..., **kwds) {
		if (!m_didcall) {
			import Traceback from deemon;
			m_didcall = true;
			local frame = Traceback()[1];
			local errout = File.stderr;
			print errout: (
				frame.file, "(", frame.line + 1, ",",
				frame.col + 1, ") : Deprecated "),;
			if (m_func is Type) {
				print errout: ("class `", m_func, "' instantiated"),;
			} else {
				print errout: ("function `", m_func, "' called"),;
			}
			if (m_message is bound)
				print errout: ":", m_message,;
			print errout:;
		}
		return m_func(args..., **kwds);
	}
}




@@An annotation that may be used wrap a function as only being allowed to be executed by a single thread at a time, as well as allow the function to be used in $with-statements
@@>import Synchronized from annotations;
@@>import SharedLock from threading;
@@>@Synchronized
@@>function myfunc() {
@@> print "Synchronized call to myfunc()";
@@>}
@@>
@@>local mylock = SharedLock();
@@>@Synchronized(mylock)
@@>function myfunc() {
@@> print "Synchronized call to myfunc()";
@@>}
@@When annotated with an explicit lock object, the annotated function is called while being surrounded by an ${operator enter} / ${operator leave} pair, thus allowing the use of a lock to restrict invocation"
class Synchronized: Callable {
	private member m_func: Callable;
	private member m_lock: Lock from threading | Object;

	@@@param lock The lock that is entered prior to invocation of @func, and will be left afterwards. When omit, a new instance of :threading.Mutex will be constructed and used in its place"
	@@Construct a new Synchronized function annotation for @func
	this(func: Callable, lock?: Lock from threading | Object) {
		m_func = func;
		if (lock is bound) {
			m_lock = lock;
		} else {
			m_lock = import("threading").SharedLock();
		}
	}

	public operator str(): string {
		return f"<Synchronized: {m_func}>";
	}
	public operator repr(): string {
		return f"Synchronized({repr m_func}, {repr m_lock})";
	}

	public operator enter(): none {
		return m_lock.operator enter();
	}
	public operator leave(): none {
		return m_lock.operator leave();
	}

	public operator () (args..., **kwds) {
		with (m_lock) {
			return m_func(args..., **kwds);
		}
	}
}




@@Annotation for timing how long a function took to execute
@@>import Timed from annotations;
@@>
@@>@Timed
@@>function myfunc() {
@@> print "In myfunc()";
@@>}
@@>
@@>myfunc();
@@>/* ??? (nanoseconds) */
@@>print repr myfunc.total.nanoseconds;
@@>
@@>myfunc();
@@>/* ??? (nanoseconds; about 2x as many; probably less due to CPU caching) */
@@>print repr myfunc.total.nanoseconds;
class Timed: Callable {
	private static member m_libtime: Module;
	private member m_func: Callable;
	private member m_total_spend: Time from time;
	private member m_num_invocations: int = 0;

	this(func: Callable) {
		m_func = func;
		if (m_libtime !is bound)
			m_libtime = import("time");
		m_total_spend = m_libtime.Time(0);
	}

	public operator str(): string {
		return f"<Timed: {m_func}>";
	}

	public operator repr(): string {
		return f"Timed({repr m_func})";
	}

	public operator () (args..., **kwds) {
		local start = m_libtime.tick();
		try {
			return m_func(args..., **kwds);
		} finally {
			m_total_spend += m_libtime.tick() - start;
			++m_num_invocations;
		}
	}

	@@Reset all timing data
	public function reset() {
		del total;
	}

	@@Get/del (reset)/set the amount of time that has
	@@already been spent while inside of the function.
	@@>print myfunc.total.nanoseconds;
	public property total: Time from time = {
		get(): Time from time {
			return m_total_spend;
		}
		del() {
			m_total_spend = m_libtime.Time(0);
			m_num_invocations = 0;
		}
		set(value: Time from time) {
			if (value is none) {
do_del_total:
				del total;
			} else if (value is int from deemon) {
				if (!value) goto do_del_total;
				m_total_spend = m_libtime.nanoseconds(value);
			} else {
				if (value !is m_libtime.Time)
					throw Error.TypeError("Expected a Time object");
				m_total_spend = copy value;
			}
		}
	}

	@@@throw ValueError The function hasn't been called yet
	@@Get the average time spent while inside of the function
	@@>print myfunc.average.nanoseconds;
	public property average: Time from time = {
		get(): Time from time {
			try {
				return m_total_spend / m_num_invocations;
			} catch (Error.ValueError.ArithmeticError.DivideByZero) {
				throw Error.ValueError(f"No profiling data available for {m_func}");
			}
		}
	}
}


/* TODO: @Typed -- Ensures that input types match declared types
 * >> import * from deemon;
 * >> import Typed from annotations;
 * >> @Typed
 * >> function foo(x: int, y: int): int {
 * >>     return x + y;
 * >> }
 * >> foo(10, 20);    // Ok
 * >> foo(10, "foo"); // Error: Argument `y' must be an `int', but is actually a `string'
 * TODO: Also extend the Overload annotation to look at argument
 *       types and try to overload depending on the argument types
 *       being passed.
 */
