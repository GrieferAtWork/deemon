#!/usr/bin/deemon
/* Copyright (c) 2018-2023 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2018-2023 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */

import * from deemon;

@@Returns a Tuple-like sequence type that where Tuple indices can
@@also be accessed as attributes, using the given @fields as names
function nameTuple(fields: {string...}): Type {
	local num_fields = #fields;
	class NameTuple: Sequence {
		private member m_values: Tuple;
		this(init) {
			/* XXX: keyword arguments? */
			if (init is Mapping) {
				m_values = Tuple.unpack(
					num_fields,
						for (local name: fields)
							init[name]);
			} else {
				m_values = Tuple.unpack(num_fields, init);
			}
		}
		operator [] (index: int) -> m_values[index];
		operator [:] (start: int, end: int) -> m_values[start:end];
		operator . (name: string) -> m_values[fields.index(name)];
		operator # () -> num_fields;
		operator contains(ob): bool -> ob in m_values;
		operator for(): Sequence {
			for (local x: m_values)
				yield x;
		}
		operator repr(): string -> repr m_values;
	};
	return NameTuple;
}


#ifdef __MAIN__

local point = nameTuple({ "x", "y" });

local x = point({ 10, 20 });

print repr x;
print x.x;
print x.y;

#endif



