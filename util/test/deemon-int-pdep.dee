#!/usr/bin/deemon
/* Copyright (c) 2018-2024 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2018-2024 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */

import * from deemon;

@@Reference implementation for @int.pdep
function correctPDep(self: int, mask: int): int {
	local result = 0;
	local resultShift = 0;
	while (mask) {
		local nSkip = mask.ctz;
		resultShift += nSkip;
		mask >>= nSkip;
		if (mask == -1) {
			result |= self << resultShift;
			break;
		}
		local nCopy = mask.ct1;
		local addend = self & nCopy.bitmask;
		result |= addend << resultShift;
		mask >>= nCopy;
		self >>= nCopy;
		resultShift += nCopy;
	}
	return result;
}

function assertPDep(self: int, mask: int) {
	local expected = correctPDep(self, mask);
//	print self.hex(), mask.hex();
	local actual = self.pdep(mask);
	assert expected == actual,
		f"\nExpected: {expected.hex()}"
		f"\nActual:   {actual.hex()}"
		f"\nself:     {self.hex()}"
		f"\nmask:     {mask.hex()}";
}

function getWideRangeOfIntegers(): {int...} {
	for (local i: {0, 14, 15, 16, 29, 30, 31, 63, 64, 65,
	               127, 128, 129, 255, 256, 257}) {
		local limit = (1 << i) - 1;
		yield -limit;
		yield -limit - 1;
		yield -limit + 1;
		yield limit;
		yield limit - 1;
		yield limit + 1;
	}
	local mask = 0xf0f0f0f0;
	for (none: [:4]) {
		yield -mask;
		yield mask;
		mask = (mask << 32) | 0xf0f0f0f0;
	}
}


assert -1 == correctPDep(-1, -1);
assert 0x1234 == correctPDep(0x1234, -1);
assert "0x3040" == correctPDep(0x1234, 0xf0f0).hex();
assert "0x3040" == correctPDep(0x34, 0xf0f0).hex();
assert "0x90a0b0c0d0e0f000" == correctPDep(0x9ABCDEF0, 0xf0f0f0f0f0f0f0f0).hex();
assert "0x90a0b0c0d0e0f000" == correctPDep(0x9ABCDEF0, 0xfffffffffffffffff0f0f0f0f0f0f0f0).hex();
assert "0x90a0b0c0d0e0f000" == correctPDep(0x123456789ABCDEF0, 0xf0f0f0f0f0f0f0f0).hex();
assert "0x102030405060708090a0b0c0d0e0f000" == correctPDep(
	0x123456789ABCDEF0, 0xf0f0f0f0f0f0f0f0f0f0f0f0f0f0f0f0).hex();
assert "0x50607080" == correctPDep(0x5678, 0xf0f0f0f0).hex();
assert "0x50607080" == correctPDep(0x12345678, 0xf0f0f0f0).hex();
assert "0x4400" == correctPDep(0x44, 0xff00).hex();
assert "0x4400" == correctPDep(0x11223344, 0xff00).hex();

assert 0x10305070 == correctPDep(0x00001357, 0xf0f0f0f0);
assert 0x00003300 == correctPDep(0x00000033, 0x0000ff00);
assertPDep(0x00001357, 0xf0f0f0f0);
assertPDep(0x00000033, 0x0000ff00);

for (local self: getWideRangeOfIntegers()) {
	for (local mask: getWideRangeOfIntegers()) {
		if (self >= 0 && mask >= 0) // TODO: REMOVE ME
		assertPDep(self, mask);
	}
}
