#!/usr/bin/deemon
/* Copyright (c) 2018-2026 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2018-2026 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */

import * from deemon;
import * from collections;

function knownGood_ffs(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int | none {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	for (local i: [start:end]) {
		if (b[i])
			return i;
	}
	return none;
}

function knownGood_ffc(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int | none {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	for (local i: [start:end]) {
		if (!b[i])
			return i;
	}
	return none;
}

function knownGood_fls(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int | none {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	for (local i: [end-1:start-1,-1]) {
		if (b[i])
			return i;
	}
	return none;
}

function knownGood_flc(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int | none {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	for (local i: [end-1:start-1,-1]) {
		if (!b[i])
			return i;
	}
	return none;
}

function knownGood_clz(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	local result = 0;
	for (local i: [end-1:start-1,-1]) {
		if (b[i])
			break;
		++result;
	}
	return result;
}

function knownGood_ctz(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	local result = 0;
	for (local i: [start:end]) {
		if (b[i])
			break;
		++result;
	}
	return result;
}

function knownGood_clo(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	local result = 0;
	for (local i: [end-1:start-1,-1]) {
		if (!b[i])
			break;
		++result;
	}
	return result;
}

function knownGood_cto(b: Bitset | Bitset.Frozen | BitsetView,
                       start: int = 0, end: int = 0): int {
	if (end < 0 || end > b.nbits)
		end = b.nbits;
	if (start > end)
		start = end;
	local result = 0;
	for (local i: [start:end]) {
		if (!b[i])
			break;
		++result;
	}
	return result;
}

function intbits(i: int): BitsetView {
	local iBytes = i.tobytes(byteorder: "little", signed: false);
	local view = BitsetView(iBytes, "w", 0, i.bitcount(signed: false));
	assert view.bytes() == iBytes;
	assert Bitset(view).bytes() == iBytes;
	return view;
}

global testedBitsetOkReadonly: {string...} = HashSet();
function assertBitsetOkReadonly(b: Bitset | BitsetView | Bitset.Frozen) {
	/* Speed up the test by only testing each bitset permuation once.
	 * This is done because our "knownGood_*" implementation (while
	 * definitely known to be good, as the name implies), are also
	 * rather slow since they are written such that it's easy to
	 * prove that they produce the correct results (as opposed to:
	 * for performance). */
	local r = repr b;
	if (!testedBitsetOkReadonly.insert(r))
		return;

	local inv = ~b;
	local nbits = b.nbits;
	assert nbits == inv.nbits;
	assert #b == b.popcount();
	assert #inv == inv.popcount();
	assert {} == (b & inv);
	assert {} == (inv & b);
	assert [:nbits] == (b | inv);
	assert [:nbits] == (inv | b);
	assert [:nbits] == (b ^ inv);
	assert [:nbits] == (inv ^ b);
	assert (#b + #inv) == nbits;
	if (b) {
		assert {} != b;
		assert (#b) > 0;
		assert b.any();
		if (b == [:nbits]) {
			assert b.all();
			assert !inv;
		}
	} else {
		assert !b.any();
		if (nbits) {
			assert !b.all();
		} else {
			assert b.all();
		}
		assert {} == b;
		assert (#b) == 0;
		assert inv == [:nbits];
	}

	for (local i: [:nbits]) {
		assert b[i] === (i in b);
		assert inv[i] === (i in inv);
		assert b[i] == !inv[i];
	}

	for (local start, end: {
		(0, -1),
		(1, -1),
		(7, -1),
		(8, -1),
		(9, -1),
		(0, nbits - 1),
		(1, nbits - 1),
		(7, nbits - 1),
		(8, nbits - 1),
		(9, nbits - 1),
		(0, {nbits - 9, -1} > ...),
		(1, {nbits - 9, -1} > ...),
		(7, {nbits - 9, -1} > ...),
		(8, {nbits - 9, -1} > ...),
		(9, {nbits - 9, -1} > ...),
	}) {
		assert knownGood_ffs(b, start, end) == b.ffs(start, end), f"{repr b}.ffs({start}, {end}) [nbits: {nbits}]";
		assert knownGood_ffc(b, start, end) == b.ffc(start, end), f"{repr b}.ffc({start}, {end}) [nbits: {nbits}]";
		assert knownGood_fls(b, start, end) == b.fls(start, end), f"{repr b}.fls({start}, {end}) [nbits: {nbits}]";
		assert knownGood_flc(b, start, end) == b.flc(start, end), f"{repr b}.flc({start}, {end}) [nbits: {nbits}]";
		assert knownGood_clz(b, start, end) == b.clz(start, end), f"{repr b}.clz({start}, {end}) [nbits: {nbits}]";
		assert knownGood_clo(b, start, end) == b.clo(start, end), f"{repr b}.clo({start}, {end}) [nbits: {nbits}]";
		assert knownGood_ctz(b, start, end) == b.ctz(start, end), f"{repr b}.ctz({start}, {end}) [nbits: {nbits}]";
		assert knownGood_cto(b, start, end) == b.cto(start, end), f"{repr b}.cto({start}, {end}) [nbits: {nbits}]";
	}
}

function assertBitsetOk(b: Bitset | BitsetView) {
	assertBitsetOkReadonly(b);
	local orig = copy b;
	assert orig == b;
	b &= b;
	assert orig == b;
	b |= b;
	assert orig == b;
	b ^= b;
	assert {} == b;
	assertBitsetOkReadonly(b);
}

function assertBitsetImpl2(expected: {int...}, actual: Bitset | BitsetView | Bitset.Frozen) {
	assert expected == actual;
	assert !(expected != actual);
	assert expected <= actual;
	assert expected >= actual;
	assert !(expected < actual);
	assert !(expected > actual);
	for (local x: expected) {
		assert actual[x];
		assert x in actual;
	}
}

function assertBitsetImpl(expected: {int...}, actual: BitsetView) {
	assert actual is BitsetView;
	assertBitsetImpl2(expected, actual);
	assertBitsetImpl2(expected, actual.frozen);
	assertBitsetImpl2(expected, Bitset(actual));
	assertBitsetImpl2(Bitset.Frozen(expected)[1:], actual[1:]);
	assertBitsetImpl2(Bitset.Frozen(expected)[1:-1], actual[1:-1]);
	assertBitsetImpl2(Bitset.Frozen(expected)[8:], actual[8:]);
}

function assertBitset(expected: {int...}, actual: BitsetView) {
	assertBitsetImpl(expected, actual);
	assertBitsetImpl(Bitset(expected), actual);
	assertBitsetImpl(Bitset(expected).frozen, actual);
	assertBitsetImpl(BitsetView(Bitset(expected)), actual);

	/* Ensure that trying to open a frozen bitset for writing throws a BufferError */
	assert (try BitsetView(actual.frozen, "w") catch (e...) e) is BufferError from errors;

	assertBitsetOk(Bitset(actual));
	assertBitsetOk(BitsetView(Bitset(actual), "w"));
	assertBitsetOkReadonly(Bitset(actual).frozen);
}

assertBitsetOk(Bitset(0));
assertBitsetOk(Bitset(15));
assertBitsetOk(Bitset(16));
assertBitsetOk(Bitset(63));
assertBitsetOk(Bitset(64));

assertBitset({},      intbits(0));
assertBitset({0},     intbits(1));
assertBitset({1},     intbits(2));
assertBitset({0,1},   intbits(3));
assertBitset([:8],    intbits(0xff));
assertBitset([1:8],   intbits(0xfe));
assertBitset([8:16],  intbits(0xff00));
assertBitset([16:24], intbits(0xff0000));
assertBitset([24:32], intbits(0xff000000));
assertBitset([4:8],   intbits(0xf0));
assertBitset([12:16], intbits(0xf000));
assertBitset([20:24], intbits(0xf00000));
assertBitset([28:32], intbits(0xf0000000));
assertBitset([:64],   intbits(0xffffffffffffffff));
assertBitset([:128],  intbits(0xffffffffffffffffffffffffffffffff));
assertBitset([:252],  intbits(0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff));
assertBitset([:256],  intbits(0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff));
assertBitset([1:256], intbits(0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffe));
assertBitset(
	{0,3,4,7,8,10,12,14,15,17,21},
	intbits(0b1000101101010110011001));
assertBitset({0,63},  intbits(0x8000000000000001));
assertBitset(
	{0,3,4,6,8,9,10,11,13,14,16,18,20,21,22,29,33,34,37,38,40,41,
	 42,43,45,46,48,50,52,53,54,57,58,59,61,62,66,69,70,77,82,84,
	 85,86,91,93,94,96,98,101,102,109,112,113,116,117,118,120,122,
	 125,126,128,129,133,134,137,140,141,142,144,146,149,150,154,
	 156,157,158,165,168,169,172,173,174,176,181,182,184,186,188,
	 189,190,192,193,197,198,200,202,205,206,208,213},
	intbits(0x216563756173207465726365732065687420646e756f6620756f59));




function assertBitsetFast(expected: {int...}, actual: BitsetView) {
	assertBitsetImpl2(expected, actual);
	assertBitsetOk(Bitset(actual));
	assertBitsetOk(BitsetView(Bitset(actual), "w"));
	assertBitsetOkReadonly(Bitset(actual).frozen);
}

function testSetAndClearImpl(b, setrange, delrange) {
	assertBitsetFast({}, b);
	setrange(b, 0, 0);
	assertBitsetFast({}, b);
	delrange(b, 0, 0);
	assertBitsetFast({}, b);
	setrange(b, 0, 1);
	assertBitsetFast({0}, b);
	setrange(b, 0, -1);
	assertBitsetFast([:31], b);
	setrange(b, 0, 32);
	assertBitsetFast([:32], b);
	delrange(b, 1, 31);
	assertBitsetFast({0,31}, b);
	delrange(b, 0, 32);
	assertBitsetFast({}, b);
	setrange(b, 9999, 9999);
	assertBitsetFast({}, b);
	setrange(b, -1, 9999);
	assertBitsetFast({31}, b);
	setrange(b, 0, 9999);
	assertBitsetFast([:32], b);
	delrange(b, -1, 9999);
	assertBitsetFast([:31], b);
	delrange(b, -8, 9999);
	assertBitsetFast([:24], b);
	delrange(b, -30, -9);
	assertBitsetFast({0,1,23}, b);
	delrange(b, -32, -31);
	assertBitsetFast({1,23}, b);
	delrange(b, -32, -30);
	assertBitsetFast({23}, b);
	b[23] = false;
	assertBitsetFast({}, b);
	b[7] = true;
	b[9] = true;
	assertBitsetFast({7,9}, b);
	del b[9];
	assertBitsetFast({7}, b);
	del b[8];
	assertBitsetFast({7}, b);
	b.set(8);
	assertBitsetFast({7,8}, b);
	b.set(10, 13);
	assertBitsetFast({7,8,10,11,12}, b);
	b.set(30, 999);
	assertBitsetFast({7,8,10,11,12,30,31}, b);
	b.clear();
	assertBitsetFast({}, b);
	b.set();
	assertBitsetFast([:32], b);
	b.flip();
	assertBitsetFast({}, b);
	b.flip();
	assertBitsetFast([:32], b);
}

function testSetAndClear(setrange, delrange) {
	testSetAndClearImpl(Bitset(32), setrange, delrange);
	testSetAndClearImpl(BitsetView(Bitset(32), "w"), setrange, delrange);
}

testSetAndClear((b, s, e) -> { b[s:e] = true; }, (b, s, e) -> { del b[s:e]; });
testSetAndClear((b, s, e) -> { b[s:e] = true; }, (b, s, e) -> { b[s:e] = false; });
testSetAndClear((b, s, e) -> { b[s:e] = true; }, (b, s, e) -> { b[s:e] = none; });
